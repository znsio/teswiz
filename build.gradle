import java.text.SimpleDateFormat

plugins {
    id "org.sonarqube" version "3.3"
    id "java"
    id "idea"
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'maven-publish'
}

group = 'com.github.znsio'
version '0.0.65'

repositories {
    mavenLocal()
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

compileJava { options.encoding = "UTF-8" }

dependencies {
    implementation fileTree(dir: "$project.projectDir/libs", include: ['*.jar'])
    implementation files("$buildDir/classes/main")
    implementation files("$buildDir/classes/test")
    implementation "com.googlecode.json-simple:json-simple:$project.googleCodeJsonSimpleVersion"
    implementation "com.konghq:unirest-java:$project.unirestVersion"
    implementation "org.assertj:assertj-core:$project.assertJVersion"
    implementation "com.github.znsio:AppiumTestDistribution:$project.atdVersion"
    implementation "org.apache.commons:commons-lang3:$project.commonsLang3Version"
    implementation "io.github.bonigarcia:webdrivermanager:$project.webDriverManagerVersion"
    implementation "com.github.vidstige:jadb:$project.jadbVersion"
    implementation "org.junit.jupiter:junit-jupiter:$project.junitVersion"
    implementation "com.epam.reportportal:agent-java-cucumber6:$project.reportportalAgentCucumberVersion"
    implementation "org.slf4j:slf4j-api:$project.slf4jVersion"
    implementation("com.epam.reportportal:agent-java-testng:$project.reportportalAgentTestNGVersion") {
        exclude group: 'com.fasterxml.jackson.core'
    }
    implementation "com.applitools:eyes-connectivity-java3-jersey1x:$project.applitoolsVersion"
    implementation("com.applitools:eyes-selenium-java3:$project.applitoolsVersion") {
        exclude group: 'java-client'
        exclude group: 'selenium-java'
        exclude group: 'eyes-connectivity-java3-jersey2x'
    }
    implementation("com.applitools:eyes-appium-java3:$project.applitoolsVersion") {
        exclude group: 'java-client'
        exclude group: 'selenium-java'
        exclude group: 'eyes-connectivity-java3-jersey2x'
    }
}

shadowJar {
    archiveBaseName.set("$project.name")
    archiveClassifier.set('')
    archiveVersion.set("$project.version")
    zip64 true
    manifest {
        attributes "Main-Class": "com.znsio.e2e.runner.Runner"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

wrapper {
    gradleVersion = project.gradleVersion // version from gradle.properties
}

publishing {
    publications {
        testing(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

task install(dependsOn: publishToMavenLocal)

static def getCurrentTimestamp ()
{
    Date today = new Date ()
    SimpleDateFormat df = new SimpleDateFormat ("MM-dd-yyyy_HH-mm-ss")
    return df.format (today)
}

task run(type: JavaExec) {
    doFirst {
        def logDirectory = "target/" + getCurrentTimestamp()
        println "Using LOG_DIR: $logDirectory"
        System.setProperty "LOG_DIR", "$logDirectory"
        environment "PLATFORM", "web"

        def configFile = System.getenv("RUN_IN_CI") ? "./src/test/resources/com/znsio/e2e/features/configs/pcloudy_config.properties" : "./src/test/resources/com/znsio/e2e/features/configs/config.properties"
        systemProperties = System.properties
        def runnerArgs = [
                "${configFile}",
                "com/znsio/e2e/steps",
                "./src/test/resources/com/znsio/e2e/features"
        ]
        args = runnerArgs
    }
    mainClass = "com.znsio.e2e.runner.Runner"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
}