import org.codehaus.groovy.runtime.GStringImpl

import java.text.SimpleDateFormat

buildscript {
    ext {
        gradleVersion = "8.12"
        applitoolsAppiumVersion = "5.73.0"
        applitoolsImagesVersion = "5.74.0"
        browserStackLocalVersion = "1.1.5"
        fasterxmlJacksonVersion = "2.18.2"
        atdVersion = "14.0.4" // ATD main branch
        jadbVersion = "1.2.1"
        unirestVersion = "3.14.5"
        specmaticVersion = '1.3.39'
        webDriverManagerVersion = '5.9.2'
        jodaTimeVersion = "2.13.0"
        masterThoughtVersion = "5.8.2"
        commonsLang3Version = "3.17.0"
        commonsRngSimpleVersion = "1.6"
        aspectJVersion = "1.9.22.1"
        assertJVersion = "3.27.0"
        jetbrainsAnnotationsVersion = "26.0.1"
        junitVersion = "5.11.4"
        snakeyamlVersion = "2.3"
        jsonDiffVersion = "0.4.16"
        pdfBoxVersion = '3.0.3'
    }
}

plugins {
    id "org.sonarqube" version "6.0.1.5171"
    id "java"
    id "idea"
    id "com.gradleup.shadow" version '8.3.5'
    id "maven-publish"
    id "io.freefair.aspectj.post-compile-weaving" version "8.11"
}

group = 'com.github.znsio'
version = '1.0.13-noATD'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenLocal()
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// Define the libs directory in the project root
ext.libsDir = file("$projectDir/libs")

def downloadDependency(String name, String type, Map<String, String> params) {
    if (!libsDir.exists()) {
        libsDir.mkdirs()
    }

    println "Download dependency: ${name} from ${type}"
    def jarFile = new File(libsDir, "${name}-${params.version}.jar")
    if (jarFile.exists()) {
        println "\tDependency $name already exists at: $jarFile. No need to redownload it"
        return jarFile
    }

    def jarUrl
    if (type == "github") {
        println "\tFetching latest GitHub release information for ${params.repoUrl}..."
        def jsonResponse = new URL(params.repoUrl).text
        def jsonSlurper = new groovy.json.JsonSlurper()
        def releaseInfo = jsonSlurper.parseText(jsonResponse)

        // Ensure assets field is a list and each asset is a Map
        def assets = releaseInfo.assets
        if (!(assets instanceof List)) {
            throw new GradleException("Assets field is not a list: $assets")
        }

        def jarAsset = releaseInfo.assets.find { it.name.matches(/${name}-\d+\.\d+\.\d+\.jar/) }
        if (!jarAsset) {
            throw new GradleException("No ${name} JAR file found in the latest GitHub release.")
        }
        jarUrl = jarAsset.browser_download_url
    } else if (type == "jitpack") {
        jarUrl = "https://jitpack.io/${params.group.replace('.', '/')}/${params.artifact}/${params.version}/${params.artifact}-${params.version}${params.fileNameSuffix}.jar"
    } else {
        throw new GradleException("Unknown type: $type")
    }

    println "\tDownloading ${name} JAR from ${jarUrl}"
    def downloadCommand
    if (System.getProperty("os.name").toLowerCase().contains("win")) {
        downloadCommand = ["cmd", "/c", "curl", "-o", jarFile.absolutePath, jarUrl]
    } else {
        downloadCommand = ["wget", "-O", jarFile.absolutePath, jarUrl]
    }
    // Explicitly convert all elements to String
    downloadCommand = downloadCommand.collect { it.toString() }

    println "\tDownloading using command: ${downloadCommand}"

    def process = new ProcessBuilder(downloadCommand).redirectErrorStream(true).start()
    process.inputStream.eachLine { println it }
    process.waitFor()

    if (process.exitValue() != 0) {
        throw new GradleException("Failed to download ${name} JAR.")
    }

    println "${name} JAR downloaded to $jarFile"
    return jarFile
}

// Define a custom task to download dependencies
task downloadDependencies {
    doLast {
        println "Downloading required dependencies..."
        def dependencies = [
                [
                        name  : "jadb",
                        type  : "jitpack",
                        params: [
                                group         : "com.github.vidstige",
                                artifact      : "jadb",
                                version       : "$project.jadbVersion",
                                fileNameSuffix: ""
                        ]
                ]
        ]

        println "\n---------------------------------------------"

        dependencies.each { dep ->
            println "Processing dependency: ${dep.name}"
            downloadDependency(dep.name, dep.type, dep.params)
            println "\n---------------------------------------------"

        }
    }
}

// Ensure dependencies are downloaded before compiling
tasks.compileJava {
    dependsOn downloadDependencies
    options.encoding = "UTF-8"
}

dependencies {
    // Local JAR dependencies
    implementation fileTree(dir: "$project.projectDir/libs", include: ['*.jar'])

    // Build-specific directories
    implementation files(layout.buildDirectory.dir("classes/main"))
    implementation files(layout.buildDirectory.dir("classes/test"))

    // Logging
    implementation "org.apache.logging.log4j:log4j-api:2.24.3"
    implementation "org.apache.logging.log4j:log4j-core:2.24.3"
//    implementation "org.apache.logging.log4j:log4j-1.2-api:2.24.3"

    // JSON and YAML processing
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$project.fasterxmlJacksonVersion"
//    implementation "org.yaml:snakeyaml:$project.snakeyamlVersion"
    implementation "com.flipkart.zjsonpatch:zjsonpatch:$project.jsonDiffVersion"

    // Assertions and testing
    implementation "org.assertj:assertj-core:$project.assertJVersion"
//    implementation "org.junit.jupiter:junit-jupiter:$project.junitVersion"
//    implementation "org.testng:testng:7.10.2"
    implementation "io.cucumber:cucumber-java:7.20.1"
    implementation "io.cucumber:cucumber-testng:7.20.1"

    // Unirest for API testing
    implementation "com.konghq:unirest-java:$project.unirestVersion"

    // Appium and BrowserStack
//    implementation "io.appium:java-client:9.3.0"
    implementation "com.browserstack:browserstack-local-java:$project.browserStackLocalVersion"

    // Applitools for visual testing
    implementation "com.applitools:eyes-images-java5:$project.applitoolsImagesVersion"
    implementation "com.applitools:eyes-appium-java5:$project.applitoolsAppiumVersion"

    // Aspect-Oriented Programming (AOP)
    implementation "org.aspectj:aspectjrt:$project.aspectJVersion"

    // Specmatic and JSON Schema validation
    implementation "in.specmatic:specmatic-core:${project.specmaticVersion}"
    implementation "org.everit.json:org.everit.json.schema:1.5.1"
    implementation 'com.jayway.jsonpath:json-path:2.9.0'

    // Utility libraries
    implementation "org.apache.commons:commons-lang3:$project.commonsLang3Version"
    implementation "org.apache.commons:commons-rng-simple:$project.commonsRngSimpleVersion"
    implementation "org.jetbrains:annotations:$project.jetbrainsAnnotationsVersion"
    implementation "joda-time:joda-time:$project.jodaTimeVersion"
    implementation "io.github.bonigarcia:webdrivermanager:$project.webDriverManagerVersion"

    // Reporting
    implementation "net.masterthought:cucumber-reporting:$masterThoughtVersion"
    implementation 'com.epam.reportportal:agent-java-cucumber6:5.3.4'

    // PDF handling
    implementation "org.apache.pdfbox:pdfbox:$project.pdfBoxVersion"

    // Lombok
    implementation 'org.projectlombok:lombok:1.18.36'
}


shadowJar {
    archiveBaseName.set("$project.name")
    archiveClassifier.set('')
    archiveVersion.set("$project.version")
    zip64 = true
    manifest {
        attributes "Multi-Release": true
        attributes "Main-Class": "com.znsio.e2e.runner.Runner"
    }
    exclude "**/Log4j2Plugins.dat"
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

wrapper {
    gradleVersion = project.gradleVersion // version from gradle.properties
}

publishing {
    publications {
        testing(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

tasks.register('install') { dependsOn publishToMavenLocal }

static def getCurrentDatestamp() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("MM-dd-yyyy")
    return df.format(today)
}

static def getMonth() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("MMM-yyyy")
    return df.format(today)
}

static def getCurrentTimestamp() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("HH-mm-ss")
    return df.format(today)
}

project.ext.logDir = "./target/" + getMonth() + "/" + getCurrentDatestamp() + "/" + getCurrentTimestamp()

def copyRpPropertiesIfMissing() {
    def rpFile = file('src/test/resources/reportportal.properties')
    def rpTemplateFile = file('src/test/resources/reportportal.properties.template')

    if (!rpFile.exists() && rpTemplateFile.exists()) {
        println "Copying $rpTemplateFile to $rpFile"
        rpFile.text = rpTemplateFile.text
    } else if (!rpTemplateFile.exists()) {
        println "$rpTemplateFile does not exist. Please create it."
    } else {
        println "$rpFile already exists."
    }
    System.setProperty("reportportal.properties.file", rpFile.absolutePath)
}

task copyRpPropertiesIfMissing {
    doLast {
        copyRpPropertiesIfMissing()
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':build') || taskGraph.hasTask(':run')) {
        copyRpPropertiesIfMissing()
    }
}

tasks.register('run', JavaExec) {
    doFirst {
        println "Using LOG_DIR: ${project.logDir}"
        System.setProperty "LOG_DIR", "${project.logDir}"
        environment "APPLITOOLS_LOG_DIR", "${project.logDir}/applitools_logs"
        environment "APPLITOOLS_API_KEY", System.getenv("TESWIZ_APPLITOOLS_API_KEY")

        def hostname = InetAddress.getLocalHost().getHostName()
        println "Hostname: $hostname"
        def ipAddress = InetAddress.getAllByName(hostname)
                .find { it.hostAddress.startsWith("192") || it.hostAddress.startsWith("172") || it.hostAddress.startsWith("10") }
                ?.hostAddress
        println "IP Address of this machine: $ipAddress"
        if (null == ipAddress) {
            println "Unable to get local IP address. NOT updating BASE_URL and REMOTE_WEBDRIVER_GRID_HOST_NAME"
        } else {
//            println "Updating BASE_URL ('http://$ipAddress:3000') and REMOTE_WEBDRIVER_GRID_HOST_NAME ('$ipAddress')"
//            environment "BASE_URL", "http://$ipAddress:3000"
            environment "REMOTE_WEBDRIVER_GRID_HOST_NAME", "$ipAddress"
        }

        def configFile = System.getenv("CONFIG")
        if (null == configFile || !file(configFile).exists()) {
            println("CONFIG file not provided, or does not exist")
            println("Run the test by providing the CONFIG file not provided, or does not exist")
            assert file(configFile).exists()
        }

//      You can also specify which config file to use based on the value of RUN_IN_CI as shown below
//
//        def isRunInCI = Boolean.parseBoolean(System.getenv("RUN_IN_CI"))
//        println "isRunningInCI: $isRunInCI"
//        def configFile = isRunInCI
//                ? "./configs/theapp/theapp_pcloudy_config.properties"
//                : "./configs/theapp/theapp_local_android_config.properties"
//        configFile = System.getenv("CONFIG") ? System.getenv("CONFIG") : configFile

        systemProperties = System.properties as Map<String, ?>
        def runnerArgs = [
                "${configFile}",
                "com/znsio/teswiz/steps",
                "./src/test/resources/com/znsio/teswiz/features"
        ]
        args = runnerArgs

        println("Debug mode: " + System.getProperty('debug', 'false'))
        // attach debugger
        // example: ./gradlew run -Ddebug=true
        if (System.getProperty('debug', 'false') == 'true') {
            println("In debug mode")
            jvmArgs '-Xdebug', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,' +
                    'address=*:5005'
        }
    }
    mainClass = "com.znsio.teswiz.runner.Runner"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
}

test {
    doFirst {
        println "Using LOG_DIR for tests: ${project.logDir}"
        def logDir = new File("${project.logDir}")
        systemProperty "LOG_DIR", "${project.logDir}"

        if (!logDir.exists()) {
            logDir.mkdirs()
            println "Created log directory at: ${logDir.absolutePath}"
        }
    }
    useTestNG()
    outputs.upToDateWhen { false }

    testLogging {
        events "started", "passed", "skipped", "failed"
        // show standard out and standard error of the test JVM(s) on the console
        showStandardStreams = true
        // show full exception trace
        exceptionFormat = 'full'
        showStackTraces = true
        showCauses = true
        showExceptions = true
    }

    // attach debugger
    if (System.getProperty('debug', 'false') == 'true') {
        jvmArgs '-Xdebug', '-agentlib:jdwp=transport=dt_socket,server=n,address=192.168.29.248:9009,suspend=y'
    }
    reports {
        html.required.set(true)
        junitXml.required.set(true)
        html.outputLocation.set(file("$project.logDir/unit-test-results"))
        junitXml.outputLocation.set(file("$project.logDir/unit-test-results"))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--release', '17', '-Xlint:deprecation']
}

tasks.withType(JavaExec).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

shadowJar.finalizedBy sourcesJar
